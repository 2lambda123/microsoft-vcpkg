cmake_minimum_required(VERSION 3.26)
project(detect_compiler NONE)

include(CheckLanguage)

set(USED_LANGUAGES C CXX CACHE STRING "Languages to abi hash the compiler for if a compiler is available")
# Supported languages: C CXX CSharp CUDA OBJC OBJCXX Fortran HIP ISPC Swift ASM ASM_NASM ASM_MARMASM ASM_MASM ASM-ATT

if(CMAKE_GENERATOR STREQUAL "Ninja" AND CMAKE_SYSTEM_NAME STREQUAL "Windows")
    foreach(lang IN LISTS USED_LANGUAGES)
      set(CMAKE_${lang}_COMPILER_WORKS 1)
      set(CMAKE_${lang}_COMPILER_FORCED 1)
    endforeach()
endif()

set(compiler_hashes "")
foreach(lang IN LISTS USED_LANGUAGES)
  check_language(${lang})
  if(CMAKE_${lang}_COMPILER)
    enable_language(${lang})
    file(SHA1 "${CMAKE_${lang}_COMPILER}" ${lang}_HASH)
    string(APPEND compiler_hashes "${lang}_HASH")
  endif()
  if(CMAKE_${lang}_HOST_COMPILER)
    enable_language(${lang})
    file(SHA1 "${CMAKE_${lang}_HOST_COMPILER}" ${lang}_HOST_HASH)
    string(APPEND compiler_hashes "${lang}_HOST_HASH")
  endif()
endforeach()

string(SHA1 COMPILER_HASH "${compiler_hashes}")

message("#COMPILER_HASH#${COMPILER_HASH}")
foreach(lang IN LISTS USED_LANGUAGES)
  if(${lang}_HASH)
    message("#COMPILER_${lang}_HASH#${${lang}_HASH}")
    message("#COMPILER_${lang}_VERSION#${CMAKE_${lang}_COMPILER_VERSION}")
    message("#COMPILER_${lang}_ID#${CMAKE_${lang}_COMPILER_ID}")
  endif()
endforeach()
