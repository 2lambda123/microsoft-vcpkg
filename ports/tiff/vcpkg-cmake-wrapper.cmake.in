cmake_policy(PUSH)
cmake_policy(SET CMP0012 NEW)
cmake_policy(SET CMP0057 NEW)
set(z_vcpkg_tiff_link_libraries "")
set(z_vcpkg_tiff_libraries "")
set(z_vcpkg_tiff_incomplete FALSE)
set(z_vcpkg_tiff_find_options "")
if("REQUIRED" IN_LIST ARGS)
    list(APPEND z_vcpkg_tiff_find_options "REQUIRED")
endif()

_find_package(${ARGS})

if(@webp@)
    find_package(WebP CONFIG ${z_vcpkg_tiff_find_options})
    if(WebP_FOUND)
        list(APPEND z_vcpkg_tiff_link_libraries WebP::WebP)
        list(APPEND z_vcpkg_tiff_libraries ${WEBP_LIBRARIES})
    else()
        set(z_vcpkg_tiff_incomplete TRUE)
    endif()
endif()
if(@lzma@)
    find_package(LibLZMA CONFIG ${z_vcpkg_tiff_find_options})
    if(LibLZMA_FOUND)
        list(APPEND z_vcpkg_tiff_link_libraries LibLZMA::LibLZMA)
        list(APPEND z_vcpkg_tiff_libraries ${LZMA_LIBRARIES})
    else()
        set(z_vcpkg_tiff_incomplete TRUE)
    endif()
endif()
if(@jpeg@)
    find_package(JPEG ${z_vcpkg_tiff_find_options})
    if(JPEG_FOUND)
        list(APPEND z_vcpkg_tiff_link_libraries JPEG::JPEG)
        list(APPEND z_vcpkg_tiff_libraries ${JPEG_LIBRARIES})
    else()
        set(z_vcpkg_tiff_incomplete TRUE)
    endif()
endif()
if(@zstd@)
    find_package(ZSTD CONFIG ${z_vcpkg_tiff_find_options})
    if(ZSTD_FOUND)
        if(TARGET zstd::libzstd_shared)
            set(z_vcpkg_tiff_zstd zstd::libzstd_shared)
        else()
            set(z_vcpkg_tiff_zstd zstd::libzstd_static)
        endif()
        list(APPEND z_vcpkg_tiff_link_libraries ${z_vcpkg_tiff_zstd})
        list(APPEND z_vcpkg_tiff_libraries ${z_vcpkg_tiff_zstd})
    else()
        set(z_vcpkg_tiff_incomplete TRUE)
    endif()
endif()
if(@zlib@)
    find_package(ZLIB ${z_vcpkg_tiff_find_options})
    if(ZLIB_FOUND)
        list(APPEND z_vcpkg_tiff_link_libraries ZLIB::ZLIB)
        list(APPEND z_vcpkg_tiff_libraries ${ZLIB_LIBRARIES})
    else()
        set(z_vcpkg_tiff_incomplete TRUE)
    endif()
endif()
if(UNIX)
    list(APPEND z_vcpkg_tiff_link_libraries m)
    list(APPEND z_vcpkg_tiff_libraries m)
endif()

if(z_vcpkg_tiff_incomplete)
    set(TIFF_FOUND FALSE)
else()
    if(TARGET TIFF::TIFF)
        set_property(TARGET TIFF::TIFF APPEND PROPERTY INTERFACE_LINK_LIBRARIES ${z_vcpkg_tiff_link_libraries})
    endif()
    if(TIFF_LIBRARIES)
        list(APPEND TIFF_LIBRARIES ${z_vcpkg_tiff_libraries})
    endif()
    if(TIFF_LIBRARY)
        list(APPEND TIFF_LIBRARY ${z_vcpkg_tiff_libraries})
    endif()
endif()
unset(z_vcpkg_tiff_link_libraries)
unset(z_vcpkg_tiff_libraries)
unset(z_vcpkg_tiff_incomplete)
unset(z_vcpkg_tiff_find_options)
cmake_policy(POP)
