cmake_policy(PUSH)
cmake_policy(SET CMP0057 NEW)
set(_tiff_find_args "${ARGS}")
set(_tiff_link_libraries "")
set(_tiff_libraries "")
set(_tiff_incomplete FALSE)
set(_tiff_find_options "")
if("REQUIRED" IN_LIST ARGS)
    list(APPEND _tiff_find_options "REQUIRED")
endif()
if(@webp@)
    find_package(WebP CONFIG ${_tiff_find_options})
    if(WebP_FOUND)
        list(APPEND _tiff_link_libraries WebP::WebP)
        list(APPEND _tiff_libraries ${WEBP_LIBRARIES})
    else()
        set(_tiff_incomplete TRUE)
    endif()
endif()
if(@lzma@)
    find_package(LibLZMA CONFIG ${_tiff_find_options})
    if(LibLZMA_FOUND)
        list(APPEND _tiff_link_libraries LibLZMA::LibLZMA)
        list(APPEND _tiff_libraries ${LZMA_LIBRARIES})
    else()
        set(_tiff_incomplete TRUE)
    endif()
endif()
if(@jpeg@)
    find_package(JPEG ${_tiff_find_options})
    if(JPEG_FOUND)
        list(APPEND _tiff_link_libraries JPEG::JPEG)
        list(APPEND _tiff_libraries ${JPEG_LIBRARIES})
    else()
        set(_tiff_incomplete TRUE)
    endif()
endif()
if(@zstd@)
    find_package(ZSTD CONFIG ${_tiff_find_options})
    if(ZSTD_FOUND)
        if(TARGET zstd::libzstd_shared)
            set(_tiff_zstd zstd::libzstd_shared)
        else()
            set(_tiff_zstd zstd::libzstd_static)
        endif()
        list(APPEND _tiff_link_libraries ${_tiff_zstd})
        list(APPEND _tiff_libraries ${_tiff_zstd})
    else()
        set(_tiff_incomplete TRUE)
    endif()
endif()
if(@zlib@)
    find_package(ZLIB ${_tiff_find_options})
    if(ZLIB_FOUND)
        list(APPEND _tiff_link_libraries ZLIB::ZLIB)
        list(APPEND _tiff_libraries ${ZLIB_LIBRARIES})
    else()
        set(_tiff_incomplete TRUE)
    endif()
endif()
if(UNIX)
    list(APPEND _tiff_link_libraries m)
    list(APPEND _tiff_libraries m)
endif()

if(_tiff_incomplete)
    set(TIFF_FOUND FALSE)
else()
    _find_package(${_tiff_find_args})
    if(TARGET TIFF::TIFF)
        set_property(TARGET TIFF::TIFF APPEND PROPERTY INTERFACE_LINK_LIBRARIES ${_tiff_link_libraries})
    endif()
    if(TIFF_LIBRARIES)
        list(APPEND TIFF_LIBRARIES ${_tiff_libraries})
    endif()
    if(TIFF_LIBRARY)
        list(APPEND TIFF_LIBRARY ${_tiff_libraries})
    endif()
endif()
unset(_tiff_find_args)
unset(_tiff_link_libraries)
unset(_tiff_libraries)
unset(_tiff_incomplete)
unset(_tiff_find_options)
cmake_policy(POP)
