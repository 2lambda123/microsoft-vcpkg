if(@PythonFinder_NEW_FINDER@)
    if(NOT DEFINED @PythonFinder_PREFIX@_FIND_REGISTRY)
        set(@PythonFinder_PREFIX@_FIND_REGISTRY "NEVER")
    endif()
endif()

if(@PythonFinder_INTERP@)
    find_program(
        @PythonFinder_PREFIX@_EXECUTABLE
        NAMES "python" "python@PYTHON_VERSION_MAJOR@.@PYTHON_VERSION_MINOR@"
        PATHS "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/tools/python3"
        NO_DEFAULT_PATH
    )
endif()

if(@PythonFinder_LIBS@)
    find_path(
        @PythonFinder_PREFIX@_INCLUDE_DIR
        NAMES "Python.h"
        PATHS "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/include"
        PATH_SUFFIXES "python@PYTHON_VERSION_MAJOR@.@PYTHON_VERSION_MINOR@"
        NO_DEFAULT_PATH
    )

    set(_PYTHON_LIB_RELEASE_VARNAME @PythonFinder_PREFIX@_LIBRARY_RELEASE)
    set(_PYTHON_LIB_DEBUG_VARNAME @PythonFinder_PREFIX@_LIBRARY_DEBUG)
    if(@PythonFinder_NEW_FINDER@)
        # Don't set the public facing hint or the finder will be unable to detect the debug library.
        # Internally, it uses the same value with an underscore prepended.
        string(PREPEND _PYTHON_LIB_RELEASE_VARNAME "_")
        string(PREPEND _PYTHON_LIB_DEBUG_VARNAME "_")
    endif()

    find_library(
        ${_PYTHON_LIB_RELEASE_VARNAME}
        NAMES
        "python@PYTHON_VERSION_MAJOR@@PYTHON_VERSION_MINOR@"
        "python@PYTHON_VERSION_MAJOR@.@PYTHON_VERSION_MINOR@"
        PATHS "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/lib"
        NO_DEFAULT_PATH
    )
    find_library(
        ${_PYTHON_LIB_DEBUG_VARNAME}
        NAMES
        "python@PYTHON_VERSION_MAJOR@@PYTHON_VERSION_MINOR@_d"
        "python@PYTHON_VERSION_MAJOR@.@PYTHON_VERSION_MINOR@d"
        PATHS "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/debug/lib"
        NO_DEFAULT_PATH
    )

    # Don't leak this mess out.
    unset(_PYTHON_LIB_RELEASE_VARNAME)
    unset(_PYTHON_LIB_DEBUG_VARNAME)
endif()

_find_package(${ARGS})

if(@PythonFinder_LIBS@)
    # Python for Windows embeds the zlib module into the core, so we have to link against it.
    # This is a separate extension module on Unix-like platforms.
    if(WIN32)
        find_package(ZLIB)
        if(@PythonFinder_PREFIX@_LIBRARIES)
            list(APPEND @PythonFinder_PREFIX@_LIBRARIES ${ZLIB_LIBRARIES})
        endif()
        if(TARGET @PythonFinder_PREFIX@::Python)
            set_property(TARGET @PythonFinder_PREFIX@::Python APPEND PROPERTY INTERFACE_LINK_LIBRARIES ZLIB::ZLIB)
        endif()
        if(TARGET @PythonFinder_PREFIX@::Module)
            set_property(TARGET @PythonFinder_PREFIX@::Module APPEND PROPERTY INTERFACE_LINK_LIBRARIES ZLIB::ZLIB)
        endif()
    else()
        find_package(Threads)
        find_library(UTIL_LIBRARY NAMES util)
        if(@PythonFinder_PREFIX@_LIBRARIES)
            list(APPEND @PythonFinder_PREFIX@_LIBRARIES ${CMAKE_THREAD_LIBS_INIT} ${CMAKE_DL_LIBS} ${UTIL_LIBRARY})
        endif()
    endif()

    if(APPLE)
        find_package(Iconv)
        find_package(Intl)
        if(TARGET @PythonFinder_PREFIX@::Python)
            get_target_property(_PYTHON_INTERFACE_LIBS @PythonFinder_PREFIX@::Python INTERFACE_LINK_LIBRARIES)
            list(REMOVE_ITEM _PYTHON_INTERFACE_LIBS "-liconv" "-lintl")
            list(APPEND _PYTHON_INTERFACE_LIBS
                Iconv::Iconv
                "$<IF:$<CONFIG:Debug>,${Intl_LIBRARY_DEBUG},${Intl_LIBRARY_RELEASE}>"
            )
            set_property(TARGET @PythonFinder_PREFIX@::Python PROPERTY INTERFACE_LINK_LIBRARIES ${_PYTHON_INTERFACE_LIBS})
            unset(_PYTHON_INTERFACE_LIBS)
        endif()
        if(TARGET @PythonFinder_PREFIX@::Module)
            get_target_property(_PYTHON_INTERFACE_LIBS @PythonFinder_PREFIX@::Module INTERFACE_LINK_LIBRARIES)
            list(REMOVE_ITEM _PYTHON_INTERFACE_LIBS "-liconv" "-lintl")
            list(APPEND _PYTHON_INTERFACE_LIBS
                Iconv::Iconv
                "$<IF:$<CONFIG:Debug>,${Intl_LIBRARY_DEBUG},${Intl_LIBRARY_RELEASE}>"
            )
            set_property(TARGET @PythonFinder_PREFIX@::Module PROPERTY INTERFACE_LINK_LIBRARIES ${_PYTHON_INTERFACE_LIBS})
            unset(_PYTHON_INTERFACE_LIBS)
        endif()
        if(@PythonFinder_PREFIX@_LIBRARIES)
            list(APPEND @PythonFinder_PREFIX@_LIBRARIES "-framework CoreFoundation" ${Iconv_LIBRARIES} ${Intl_LIBRARIES})
        endif()
    endif()
endif()
