add_library(tensorflow_cc::tensorflow_cc SHARED IMPORTED)

# Compute the installation prefix from this tensorflow-cc-config.cmake file location.
# CMAKE_CURRENT_LIST_DIR = [vcpkg_installed_dir]/[target_triplet]/share/tensorflow-cc
get_filename_component(TENSORFLOW_INSTALL_PREFIX "${CMAKE_CURRENT_LIST_DIR}" DIRECTORY)
# TENSORFLOW_INSTALL_PREFIX = [vcpkg_installed_dir]/[target_triplet]/share
get_filename_component(TENSORFLOW_INSTALL_PREFIX "${TENSORFLOW_INSTALL_PREFIX}" DIRECTORY)
# TENSORFLOW_INSTALL_PREFIX = [vcpkg_installed_dir]/[target_triplet]

set_property(TARGET tensorflow_cc::tensorflow_cc
	PROPERTY INTERFACE_INCLUDE_DIRECTORIES
		"${TENSORFLOW_INSTALL_PREFIX}/include"
		"${TENSORFLOW_INSTALL_PREFIX}/include/tensorflow-external"
		"${TENSORFLOW_INSTALL_PREFIX}/include/tensorflow-external/src"
)

if(@TENSORFLOW_HAS_RELEASE@)
	set_property(TARGET tensorflow_cc::tensorflow_cc
		APPEND PROPERTY IMPORTED_CONFIGURATIONS
			RELEASE
	)

	set_property(TARGET tensorflow_cc::tensorflow_cc
		PROPERTY IMPORTED_LOCATION_RELEASE
			"${TENSORFLOW_INSTALL_PREFIX}/bin/tensorflow_cc.dll"
	)
	set_property(TARGET tensorflow_cc::tensorflow_cc
		PROPERTY IMPORTED_IMPLIB_RELEASE
			"${TENSORFLOW_INSTALL_PREFIX}/lib/tensorflow_cc.lib"
	)
endif()

if(@TENSORFLOW_HAS_DEBUG@)
	set_property(TARGET tensorflow_cc::tensorflow_cc
		APPEND PROPERTY IMPORTED_CONFIGURATIONS DEBUG
	)

	set_property(TARGET tensorflow_cc::tensorflow_cc
		PROPERTY IMPORTED_LOCATION_DEBUG
			"${TENSORFLOW_INSTALL_PREFIX}/debug/bin/tensorflow_cc.dll"
	)
	set_property(TARGET tensorflow_cc::tensorflow_cc
		PROPERTY IMPORTED_IMPLIB_DEBUG
			"${TENSORFLOW_INSTALL_PREFIX}/debug/lib/tensorflow_cc.lib"
	)
endif()

set(tensorflow_cc_FOUND TRUE)
