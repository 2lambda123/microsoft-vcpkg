# Compute the installation prefix relative to this file.
get_filename_component(_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
if(_IMPORT_PREFIX STREQUAL "/")
  set(_IMPORT_PREFIX "")
endif()

add_library(crashpad INTERFACE)
add_library(crashpad::crashpad ALIAS crashpad)

set(CRASHPAD_LIBRARIES client common util base)

if(WIN32)
	target_compile_definitions(crashpad INTERFACE NOMINMAX)
elseif(APPLE)
	list(APPEND CRASHPAD_LIBRARIES ApplicationServices
        CoreFoundation Foundation IOKit Security bsm)
endif()

if(CMAKE_BUILD_TYPE STREQUAL "Debug")
  set(_IMPORT_LIB_DIR "${_IMPORT_PREFIX}/debug/lib")
else()
  set(_IMPORT_LIB_DIR "${_IMPORT_PREFIX}/lib")
endif()

foreach(LIB_NAME ${CRASHPAD_LIBRARIES})
  find_library(
    _LIB 
    NAMES ${LIB_NAME} 
    PATHS ${_IMPORT_LIB_DIR}
    REQUIRED 
    NO_DEFAULT_PATH)
  target_link_libraries(crashpad INTERFACE ${_LIB})
  unset(_LIB CACHE)
endforeach()

find_package(ZLIB REQUIRED)
target_link_libraries(crashpad INTERFACE ZLIB::ZLIB)

# Crashpad Headers
target_include_directories(crashpad INTERFACE 
        ${_IMPORT_PREFIX}/include/crashpad 
        ${_IMPORT_PREFIX}/include/crashpad/third_party/mini_chromium/mini_chromium
)
