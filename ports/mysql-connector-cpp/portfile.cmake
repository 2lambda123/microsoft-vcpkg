vcpkg_from_github(
    OUT_SOURCE_PATH SOURCE_PATH
    REPO mysql/mysql-connector-cpp
    REF 1.1.13
    SHA512 76cf3d8c9dbaa006ccbb3c672538c540167c7a9cf3406a371313dc346598b119515f25fea2a786bb8cab12d96fd607b69f320a6d897aaeeb668eff831920fb5f
    HEAD_REF master
    PATCHES
        fix-static-build.patch
)

set(MYSQL_INCLUDE_DIR "${CURRENT_INSTALLED_DIR}/include/mysql")
find_library(MYSQL_LIB NAMES libmysql mysqlclient PATH_SUFFIXES lib PATHS "${CURRENT_INSTALLED_DIR}" NO_DEFAULT_PATH REQUIRED)
find_library(MYSQL_LIB_DEBUG NAMES libmysql mysqlclient PATH_SUFFIXES lib PATHS "${CURRENT_INSTALLED_DIR}/debug" NO_DEFAULT_PATH REQUIRED)

set(CONFIGURE_OPTS)
set(OPTIONS)
string(COMPARE EQUAL "${VCPKG_LIBRARY_LINKAGE}" "static" MYSQLCLIENT_STATIC_LINKING)
if(VCPKG_TARGET_IS_WINDOWS)
    set(CONFIGURE_OPTS WINDOWS_USE_MSBUILD)
    string(COMPARE EQUAL "${VCPKG_CRT_LINKAGE}" "static" STATIC_CRT)
    list(APPEND OPTIONS -DSTATIC_CRT=${STATIC_CRT})
else()
    string(COMPARE EQUAL "${VCPKG_LIBRARY_LINKAGE}" "dynamic" ENABLE_BUILD_DYNAMIC)
    list(APPEND OPTIONS -DENABLE_BUILD_DYNAMIC=${ENABLE_BUILD_DYNAMIC})
    list(APPEND OPTIONS -DENABLE_BUILD_STATIC=${MYSQLCLIENT_STATIC_LINKING})
endif()
list(APPEND OPTIONS -DSTATIC_ONLY=${MYSQLCLIENT_STATIC_LINKING})
list(APPEND OPTIONS -DMYSQLCLIENT_STATIC_LINKING=${MYSQLCLIENT_STATIC_LINKING})

set(CMAKE_SYSTEM_NAME ${VCPKG_CMAKE_SYSTEM_NAME})
set(CMAKE_SYSTEM_VERSION 0)
if(DEFINED VCPKG_CMAKE_SYSTEM_VERSION)
    set(CMAKE_SYSTEM_VERSION ${VCPKG_CMAKE_SYSTEM_VERSION})
endif()

# Use mysql-connector-cpp's own build process, skipping examples and tests.
vcpkg_cmake_configure(
    SOURCE_PATH ${SOURCE_PATH}
    ${CONFIGURE_OPTS}
    OPTIONS
        -DMYSQL_INCLUDE_DIR=${MYSQL_INCLUDE_DIR}
        -DMYSQLCPPCONN_BUILD_EXAMPLES:BOOL=OFF
        -DMYSQLCPPCONN_BUILD_TESTS:BOOL=OFF
        -DCMAKE_SYSTEM_VERSION=${CMAKE_SYSTEM_VERSION}
        ${OPTIONS}
    OPTIONS_RELEASE
        -DMYSQL_LIB_DIR=${CURRENT_INSTALLED_DIR}/lib
        -DMYSQL_LIB=${MYSQL_LIB}
    OPTIONS_DEBUG
        -DMYSQL_LIB_DIR=${CURRENT_INSTALLED_DIR}/debug/lib
        -DMYSQL_LIB=${MYSQL_LIB_DEBUG}
    MAYBE_UNUSED_VARIABLES
        STATIC_CRT
)

vcpkg_cmake_install()

file(GLOB DLLS ${CURRENT_PACKAGES_DIR}/lib/*.dll)
if(DLLS)
    file(COPY ${DLLS} DESTINATION ${CURRENT_PACKAGES_DIR}/bin)
    file(REMOVE ${DLLS})
endif()
file(REMOVE ${CURRENT_PACKAGES_DIR}/BUILDINFO ${CURRENT_PACKAGES_DIR}/LICENSE ${CURRENT_PACKAGES_DIR}/README)

file(REMOVE_RECURSE ${CURRENT_PACKAGES_DIR}/debug/include)
file(GLOB DLLS ${CURRENT_PACKAGES_DIR}/debug/lib/*.dll)
if(DLLS)
    file(COPY ${DLLS} DESTINATION ${CURRENT_PACKAGES_DIR}/debug/bin)
    file(REMOVE ${DLLS})
endif()
file(REMOVE ${CURRENT_PACKAGES_DIR}/debug/BUILDINFO ${CURRENT_PACKAGES_DIR}/debug/LICENSE ${CURRENT_PACKAGES_DIR}/debug/README)

# Handle copyright
file(INSTALL ${SOURCE_PATH}/LICENSE DESTINATION ${CURRENT_PACKAGES_DIR}/share/${PORT} RENAME copyright)
