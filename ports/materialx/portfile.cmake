vcpkg_from_github(
    OUT_SOURCE_PATH SOURCE_PATH
    REPO AcademySoftwareFoundation/MaterialX
    REF ef98d9e8b4705b855f97afd23ee1fe5fb66cd736 # 1.38.4
    SHA512 06222973a253aa2ce0b6c1cfca97941c0896cf8e0639551b7f23331e5ae7cf623a785e544fedda24fa7fcdd2bf86f6e83152a5199d1545f0e50b4836e7cbd769
    HEAD_REF main
    PATCHES
        001-fix-MaterialXCore-generated-header.patch
)

if(VCPKG_LIBRARY_LINKAGE STREQUAL "dynamic")
    set(OPTION_BUILD_SHARED_LIB ON)
else()
    set(OPTION_BUILD_SHARED_LIB OFF)
endif()

vcpkg_cmake_configure(
    SOURCE_PATH ${SOURCE_PATH}
    OPTIONS
        -DMATERIALX_BUILD_SHARED_LIBS:BOOL=${OPTION_BUILD_SHARED_LIB}

        -DMATERIALX_BUILD_JS:BOOL=NO
        -DMATERIALX_BUILD_PYTHON:BOOL=NO
        -DMATERIALX_BUILD_VIEWER:BOOL=NO
        -DMATERIALX_BUILD_DOCS:BOOL=NO
        -DMATERIALX_BUILD_GEN_GLSL:BOOL=NO
        -DMATERIALX_BUILD_GEN_OSL:BOOL=NO
        -DMATERIALX_BUILD_GEN_MDL:BOOL=NO
        -DMATERIALX_BUILD_RENDER:BOOL=NO
        -DMATERIALX_BUILD_OIIO:BOOL=NO
        -DMATERIALX_BUILD_TESTS:BOOL=NO

        -DMATERIALX_PYTHON_LTO:BOOL=NO
        -DMATERIALX_INSTALL_PYTHON:BOOL=NO
        -DMATERIALX_TEST_RENDER:BOOL=NO
        -DMATERIALX_WARNINGS_AS_ERRORS:BOOL=NO
    MAYBE_UNUSED_VARIABLES
        MATERIALX_PYTHON_LTO # Used when MATERIALX_BUILD_PYTHON=ON
)

vcpkg_cmake_install()

vcpkg_copy_pdbs()
vcpkg_cmake_config_fixup(PACKAGE_NAME MaterialX CONFIG_PATH lib/cmake/MaterialX)

file(REMOVE_RECURSE
    ${CURRENT_PACKAGES_DIR}/CHANGELOG.md
    ${CURRENT_PACKAGES_DIR}/LICENSE
    ${CURRENT_PACKAGES_DIR}/README.md
    ${CURRENT_PACKAGES_DIR}/THIRD-PARTY.md
    ${CURRENT_PACKAGES_DIR}/debug/CHANGELOG.md
    ${CURRENT_PACKAGES_DIR}/debug/LICENSE
    ${CURRENT_PACKAGES_DIR}/debug/README.md
    ${CURRENT_PACKAGES_DIR}/debug/THIRD-PARTY.md
    ${CURRENT_PACKAGES_DIR}/debug/include)

# Handle copyright
configure_file(${SOURCE_PATH}/LICENSE ${CURRENT_PACKAGES_DIR}/share/${PORT}/copyright COPYONLY)
