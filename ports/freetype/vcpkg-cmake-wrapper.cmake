get_filename_component(vcpkg_freetype_prefix "${CMAKE_CURRENT_LIST_DIR}" DIRECTORY)
get_filename_component(vcpkg_freetype_prefix "${vcpkg_freetype_prefix}" DIRECTORY)
find_path(FREETYPE_INCLUDE_DIR_ft2build ft2build.h PATHS "${vcpkg_freetype_prefix}" PATH_SUFFIXES include NO_DEFAULT_PATH)
find_path(FREETYPE_INCLUDE_DIR_freetype2 config/ftheader.h PATHS "${vcpkg_freetype_prefix}" PATH_SUFFIXES include/freetype NO_DEFAULT_PATH)
find_path(FREETYPE_INCLUDE_DIR bzlib.h PATHS "${vcpkg_freetype_prefix}" PATH_SUFFIXES include NO_DEFAULT_PATH)
find_library(FREETYPE_LIBRARY_RELEASE NAMES freetype libfreetype NAMES_PER_DIR PATHS "${vcpkg_freetype_prefix}" PATH_SUFFIXES lib NO_DEFAULT_PATH)
find_library(FREETYPE_LIBRARY_DEBUG NAMES freetyped libfreetyped freetype libfreetype NAMES_PER_DIR PATHS "${vcpkg_freetype_prefix}" PATH_SUFFIXES debug/lib lib NO_DEFAULT_PATH)
if(CMAKE_VERSION VERSION_LESS "3.7")
    find_library(FREETYPE_LIBRARY NAMES freetyped libfreetyped freetype libfreetype NAMES_PER_DIR)
endif()
unset(vcpkg_freetype_prefix)
_find_package(${ARGS})

cmake_policy(PUSH)
cmake_policy(SET CMP0012 NEW)

if(@FT_WITH_ZLIB@)
    find_package(ZLIB)
endif()

if(@FT_WITH_BZIP2@)
    find_package(BZip2)
endif()

if(@FT_WITH_PNG@)
    find_package(PNG)
endif()

if(@FT_WITH_BROTLI@)
    find_library(BROTLIDEC_LIBRARY_RELEASE NAMES brotlidec brotlidec-static PATHS "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}" PATH_SUFFIXES lib NAMES_PER_DIR NO_DEFAULT_PATH)
    find_library(BROTLIDEC_LIBRARY_DEBUG NAMES brotlidec brotlidec-static brotlidecd brotlidec-staticd PATHS "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/debug" PATH_SUFFIXES lib NAMES_PER_DIR NO_DEFAULT_PATH)
    find_library(BROTLICOMMON_LIBRARY_RELEASE NAMES brotlicommon brotlicommon-static PATHS "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}" PATH_SUFFIXES lib NAMES_PER_DIR NO_DEFAULT_PATH)
    find_library(BROTLICOMMON_LIBRARY_DEBUG NAMES brotlicommon brotlicommon-static brotlicommond brotlicommon-staticd PATHS "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/debug" PATH_SUFFIXES lib NAMES_PER_DIR NO_DEFAULT_PATH)
    include(SelectLibraryConfigurations)
    select_library_configurations(BROTLIDEC)
    select_library_configurations(BROTLICOMMON)
endif(@FT_WITH_BROTLI@)

if(TARGET Freetype::Freetype)
    if(@FT_WITH_ZLIB@)
        set_property(TARGET Freetype::Freetype APPEND PROPERTY INTERFACE_LINK_LIBRARIES ZLIB::ZLIB)
    endif()

    if(@FT_WITH_BZIP2@)
        set_property(TARGET Freetype::Freetype APPEND PROPERTY INTERFACE_LINK_LIBRARIES BZip2::BZip2)
    endif()

    if(@FT_WITH_PNG@)
        set_property(TARGET Freetype::Freetype APPEND PROPERTY INTERFACE_LINK_LIBRARIES PNG::PNG)
    endif()
    if(@FT_WITH_BROTLI@)
        if(BROTLIDEC_LIBRARY_DEBUG)
            set_property(TARGET Freetype::Freetype APPEND PROPERTY INTERFACE_LINK_LIBRARIES $<$<CONFIG:DEBUG>:${BROTLIDEC_LIBRARY_DEBUG}>)
            set_property(TARGET Freetype::Freetype APPEND PROPERTY INTERFACE_LINK_LIBRARIES $<$<CONFIG:DEBUG>:${BROTLICOMMON_LIBRARY_DEBUG}>)
        endif()
        if(BROTLIDEC_LIBRARY_RELEASE)
            set_property(TARGET Freetype::Freetype APPEND PROPERTY INTERFACE_LINK_LIBRARIES $<$<NOT:$<CONFIG:DEBUG>>:${BROTLIDEC_LIBRARY_RELEASE}>)
            set_property(TARGET Freetype::Freetype APPEND PROPERTY INTERFACE_LINK_LIBRARIES $<$<NOT:$<CONFIG:DEBUG>>:${BROTLICOMMON_LIBRARY_RELEASE}>)
        endif()
    endif()
endif()

if(FREETYPE_LIBRARIES)
    if(@FT_WITH_ZLIB@)
        list(APPEND FREETYPE_LIBRARIES ${ZLIB_LIBRARIES})
    endif()

    if(@FT_WITH_BZIP2@)
        list(APPEND FREETYPE_LIBRARIES ${BZIP2_LIBRARIES})
    endif()

    if(@FT_WITH_PNG@)
        list(APPEND FREETYPE_LIBRARIES ${PNG_LIBRARIES})
    endif()

    if(@FT_WITH_BROTLI@)
        list(APPEND FREETYPE_LIBRARIES ${BROTLIDEC_LIBRARIES} ${BROTLICOMMON_LIBRARIES})
    endif()
endif()
cmake_policy(POP)
